grammar de.atb.typhondl.xtext.TyphonDL with org.eclipse.xtext.common.Terminals

//import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate typhonDL "http://www.typhon.org/dsls/xtext/TyphonDL"

DeploymentModel:
    (elements+=Element)*
;

Element: 
	Deployment | Type | Database
;
   
Type:
    PlatformType | ContainerType | DBType
;

Database:
	Mongo | MariaDB
;

Mongo:
	'mongo' name=ID '{'
	(mandatoryEntry=MongoMandatoryEntry)
	(optionalEntries+=MongoOptional)*
	'}'
;

MariaDB:
	'mariaDB' name=ID '{'
	(mandatoryEntry=MariaDBMandatoryEntry)
	(optionalEntries+=MariaDBOptional)*
	'}'
;

MongoMandatoryEntry:
	'userName' '=' userName=Value ';'
	'password' '=' password=Value ';'
;

MongoOptional:
	Ports | Volumes | RootPassword | DBName | Key_Value
;


MariaDBMandatoryEntry:
	'userName' '=' userName=Value ';'
	'password' '=' password=Value ';'
	'rootPassword' '=' rootPassword=Value ';'
;

MariaDBOptional:
	Ports | Volumes | DBName | Key_Value
;

UserName:
	'userName' '=' userName=Value ';'
;

Password:
	'password' '=' password=Value ';'
;

DBName:
	'dbname' '=' dbname=Value ';'
;

RootPassword:
	'rootPassword' '=' rootPassword=Value ';'
;

Volumes:
	'volumes' '=' volumes=Value ';'
;

Ports:
	'ports' '=' ports=Value ';'
;

PlatformType:
	'platformtype' name=ID
;
	
ContainerType:
	'containertype' name=ID
;
	
DBType:
	'dbtype' name=ID
;

Deployment :
	{Deployment}
	'platform' name=ID ':' type=[PlatformType] '{'
     (clusters+=Cluster)*
     '}'
;

Cluster :
	{Cluster}
	'cluster' name=ID '{'
		(applications+=Application)*
     '}'
;

Application :
	{Application}
	'application' name=ID '{'
		(containers+=Container)*     
     '}'
;

Container :
	{Container}
	'container' name=ID ':' type=[ContainerType]'{'
		'database' ':' (database=[Database])
		(properties+=Property | features+=Feature)*
	'}'
;

Property :
	Key_Value | Key_ValueArray | Key_ValueList
;

Key_Value : 
	name=ID '=' value=Value';'
;

Key_ValueList:
	name=ID '{' (environmentVars+=QSTRING)+ '}'
;

Key_ValueArray : 
	name=ID '['
		(value=Value)(','values+=Value)*
	']'
;

Value : 
	(STRING|ID|INT|'/'|':'|'-'|'.'|'=')+
;

Feature:
    name=ID ':' type=[Element]
;

terminal QSTRING:
			'"' ('A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'=')* '"'
;