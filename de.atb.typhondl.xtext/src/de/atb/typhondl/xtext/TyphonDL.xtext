grammar de.atb.typhondl.xtext.TyphonDL with org.eclipse.xtext.common.Terminals

//import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate typhonDL "http://www.typhon.org/dsls/xtext/TyphonDL"

DeploymentModel:
    (elements+=Element)*
;

Element: 
	Deployment | Type | DB
;
   
Type:
    PlatformType | ContainerType | DBType
;

SupportedDBMS:
	MariaDB | Mongo
;

Mongo:
	'mongo' name=ID '{'
	(image=IMAGE)
	(userName=MONGO_INITDB_ROOT_USERNAME)
	(password=MONGO_INITDB_ROOT_PASSWORD)
	'}'
;

IMAGE:
	'image' '=' value=Value ';'
;

MONGO_INITDB_ROOT_PASSWORD:
	'MONGO_INITDB_ROOT_PASSWORD' '=' value=Value ';'
;

MONGO_INITDB_ROOT_USERNAME:
	'MONGO_INITDB_ROOT_USERNAME' '=' value=Value ';'
;

MariaDB:
	'mariaDB' name=ID '{'
	(image=IMAGE)
	(userName=MYSQL_USER)
	(password=MYSQL_PASSWORD)
	(rootPassword=MYSQL_ROOT_PASSWORD)
	(DBname=MYSQL_DATABASE)
	'}'
;

MYSQL_USER:
	'MYSQL_USER' '=' value=Value ';'
;

MYSQL_DATABASE:
	'MYSQL_DATABASE' '=' value=Value ';'
;

MYSQL_PASSWORD:
	'MYSQL_PASSWORD' '=' value=Value ';'
;

MYSQL_ROOT_PASSWORD:
	'MYSQL_ROOT_PASSWORD' '=' value=Value ';'
;

PlatformType:
	'platformtype' name=ID
;
	
ContainerType:
	'containertype' name=ID
;
	
DBType:
	'dbtype' name=ID
;

DB:
	'databases' '{'
	(dbs+=SupportedDBMS)+
	'}'
;

Deployment :
	{Deployment}
	'platform' name=ID ':' type=[PlatformType] '{'
     (clusters+=Cluster)*
     '}'
;

Cluster :
	{Cluster}
	'cluster' name=ID '{'
		(applications+=Application)*
     '}'
;

Application :
	{Application}
	'application' name=ID '{'
		(containers+=Container)*     
     '}'
;

Container :
	{Container}
	'container' name=ID ':' type=[ContainerType]'{'
		'database' ':' (database=[SupportedDBMS])
		(properties+=Property | features+=Feature)*
	'}'
;

Property :
	Key_Value | Key_ValueArray | Key_ValueList
;

Key_Value : 
	name=ID '=' value=Value';'
;

Key_ValueList:
	name=ID '{' (environmentVars+=QSTRING)+ '}'
;

Key_ValueArray : 
	name=ID '['
		(value=Value)(','values+=Value)*
	']'
;

Value : 
	(STRING|ID|INT|'/'|':'|'-'|'.'|'=')+
;

Feature:
    name=ID ':' type=[Element]
;

terminal QSTRING:
			'"' ('A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'=')* '"'
;