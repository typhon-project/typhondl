grammar de.atb.typhondl.xtext.TyphonDL with org.eclipse.xtext.common.Terminals

//import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate typhonDL "http://www.typhon.org/dsls/xtext/TyphonDL"

DeploymentModel:
	(guiMetaInformation+=Import)*
    (elements+=Element)*
;

Element: 
	Deployment | Type | Software
;

Import:
	'import' relativePath=Value 'as' name=ID
;

Software:
	DB | NonDB
;

NonDB:
	'software' name=ID '{'
	reference=QSTRING
	'}'
;
   
Type:
    PlatformType | ContainerType | DBType
;

DB:
	'database' name=ID ':' type=[DBType] '{'
	(image=IMAGE)
	(parameters+=Property)*
	'}'
;

IMAGE:
	'image' '=' value=Value ';'
;

PlatformType:
	'platformtype' name=ID
;
	
ContainerType:
	'containertype' name=ID
;
	
DBType:
	'dbtype' name=ID
;

Deployment :
	{Deployment}
	'platform' name=ID ':' type=[PlatformType] '{'
     (clusters+=Cluster)*
     '}'
;

Cluster :
	{Cluster}
	'cluster' name=ID '{'
		(applications+=Application)*
     '}'
;

Application :
	{Application}
	'application' name=ID '{'
		(containers+=Container)*     
     '}'
;

Container :
	{Container}
	'container' name=ID ':' type=[ContainerType]'{'
		(references+=[Software])*
		(properties+=Property)*
	'}'
;

Property :
	Key_Value | Key_ValueArray | Key_ValueList
;

Key_Value : 
	name=ID '=' value=Value';'
;

Key_ValueList:
	name=ID '{' (environmentVars+=QSTRING)+ '}'
;

Key_ValueArray : 
	name=ID '['
		(value=Value)(','values+=Value)*
	']'
;

Value : 
	(STRING|ID|INT|'/'|':'|'-'|'.'|'=')+
;

terminal QSTRING:
			'"' ('A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'=')* '"'
;