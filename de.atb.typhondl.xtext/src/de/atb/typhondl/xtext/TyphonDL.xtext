grammar de.atb.typhondl.xtext.TyphonDL with org.eclipse.xtext.common.Terminals

//import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate typhonDL "http://www.typhon.org/dsls/xtext/TyphonDL"

DeploymentModel:
    (guiMetaInformation+=MetaModel)*
    (elements+=Model)*
;

MetaModel:
    Import
;

Model: 
    Platform | Type | Software
;

Import:
    'import' relativePath=Value
;


Software:
	DB | NonDB
;

NonDB:
	'software' name=ID '{'
	(image=IMAGE)
	(parameters+=Property)*
	'}'
;
   
Type:
    PlatformType | ContainerType | DBType
;

DB:
	'database' name=ID ':' type=[DBType] '{'
	(image=IMAGE)
	(parameters+=Property)*
	'}'
;

IMAGE:
	'image' '=' value=Value ';'
;

PlatformType:
	'platformtype' name=ID
;
	
ContainerType:
	'containertype' name=ID
;
	
DBType:
	'dbtype' name=ID
;

Platform :
	{Platform}
	'platform' name=ID ':' type=[PlatformType] '{'
     (clusters+=Cluster)* 
     '}'
;

Cluster :
	{Cluster}
	'cluster' name=ID '{'
		(networks+=Cluster_Network)*
		(properties+=Property)*
		(applications+=Application)*		
     '}'
;

Cluster_Network:
	'network' name=ID 
		('{'
		(key_values+=Key_Value)* 
		'}')?
;

Container_Network:
	'networks' reference=[Cluster_Network] (',' references+=[Cluster_Network])*
;

Application :
	{Application}
	'application' name=ID '{'
		(containers+=Container)*     
     '}'
;

Container :
	{Container}
	'container' name=ID ':' type=[ContainerType]'{'
		(deploys=Reference)?
		(depends_on+=Dependency)*
		(networks=Container_Network)?
		(properties+=Property)*
	'}'
;

Dependency:
	'depends_on' reference=[Container]
;

Reference:
	'deploys' reference=[Software]
;

Property :
	Key_Value | Key_ValueArray | Key_KeyValueList
;

Key_Value : 
	name=ID '=' value=Value ';'
;

Key_KeyValueList:
	name=ID '{' (key_Values+=Key_Value)+ '}'
;

Key_ValueArray : 
	name=ID '['
		(value+=Value)','(values+=Value)*
	']'
;

Value : 
	(STRING|ID|INT|'/'|':'|'-'|'.'|'=')+
;

terminal QSTRING:
			'"' ('A'..'Z') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'=')* '"'
;